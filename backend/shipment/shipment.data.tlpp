#include "tlpp-core.th"
#include "FWMVCDEF.CH"

namespace shipment

//------------------------------------------------------------------------------
/*/{Protheus.doc} ShipmentData
	Manipula os dados de shipment
	@type		class
	@author		Alberto.Gonçalves
	@since		20/10/2024
/*/
//------------------------------------------------------------------------------
Class ShipmentData From FWAdapterBaseV2
    Public Data oHashFields	as Object

    Public method new() as Object
    Public method get()
    Public method handleShipment() as Logical
    Public method setHashFields()
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} ShipmentData:new()
	Obtém uma nova instância da classe ShipmentData
	@type		method
	@author		Alberto.Gonçalves
	@since		20/10/2024
/*/
//------------------------------------------------------------------------------
Method new(cVerb as Character) as Object Class ShipmentData
    _Super:new(cVerb, .T.)
Return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc} ShipmentData::setHashFields
    Define os campos utilizados na operação de Incluir e Alterar.
	@type		method
	@author		Alberto.Gonçalves
	@since		06/10/2022
/*/
//------------------------------------------------------------------------------
METHOD setHashFields() CLASS ShipmentData
	::oHashFields := tHashMap():new()

	::oHashFields:Set("filial", "ZZ4_FILIAL")
	::oHashFields:Set("code", "ZZ4_CODE")
	::oHashFields:Set("status", "ZZ4_STATUS")
	::oHashFields:Set("dtini", "ZZ4_DTINI")
	::oHashFields:Set("dtexpe", "ZZ4_DTEXPE")
	::oHashFields:Set("dtcheg", "ZZ4_DTCHEG")
	::oHashFields:Set("destin", "ZZ4_DESTIN")
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} ShipmentData:get()
	Retorna os dados de shipment
	@type		method
	@author		Alberto.Gonçalves
	@since		20/10/2024
/*/
//------------------------------------------------------------------------------
Method get(nPage as Numeric, nPageSize as Numeric, cFilter as Character,;
    cFields as Character, cSort as Character, cShipmentCode as Character) Class ShipmentData
    Local aArea as Array
    Local cWhere as Character
    Local aFilter as Array

    aFilter := {}

    If Empty(cShipmentCode)
        cWhere := "ZZ4_FILIAL = '" + FwxFilial("ZZ4") + "' AND D_E_L_E_T_ = ''"
    Else
        cWhere := "ZZ4_CODE = '" + cShipmentCode + "' AND ZZ4_FILIAL = '" + FwxFilial("ZZ4") + "' AND D_E_L_E_T_ = ''"
    EndIf

    aArea   := FwGetArea()

    AddMapFields(self)

    If !Empty(cFilter)
        aAdd(aFilter, {"filter", cFilter})
        ::SetUrlFilter(aFilter)
    EndIf

    If !Empty(cFields)
        ::SetFields(cFields)
    EndIf

    If !Empty(cSort)
        ::SetOrderQuery(cSort)
    EndIf

    ::SetPage(nPage)
    ::SetPageSize(nPageSize)
    ::SetQuery(utils.u_getQuery('ZZ4'))
    ::SetWhere(cWhere)
    ::SetOrder("ZZ4_FILIAL, ZZ4_CODE")

    If ::Execute()
        ::FillGetResponse()
    EndIf

    RestArea( aArea )
	aSize( aArea, 0 )
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} ShipmentData:handleShipment()
	Inclui, altera ou exclui um tutor
	@type		method
	@author		Alberto.Gonçalves
	@since		20/10/2024
/*/
//------------------------------------------------------------------------------
Method handleShipment(nOperation as Numeric, jBody as Json, cShipmentCode as Character) as Logical Class ShipmentData
    Local oModel	as Object
	Local oModelZZ4	as Object
    Local lRet as Logical
    Local aData as Array
    
    lRet := .T.
    ::setHashFields()

    If nOperation <> 5
        aData := utils.u_buildData(jBody, self)
	Endif

    If nOperation <> 3
        If !utils.u_searchRecord(cShipmentCode, 'ZZ4')
            lRet := .F.
            Return SetResponse(lRet)
        EndIf
    EndIf

    oModel := FWLoadModel("ZZ4B_MVC")
    oModelZZ4 := oModel:GetModel("ZZ4MASTER")
    utils.u_setOperationModel(oModel, nOperation)

    oModel:Activate()
    If oModel:IsActive()
        If oModel:GetOperation() <> MODEL_OPERATION_DELETE
            lRet := utils.u_setValue(aData, oModelZZ4, 'ZZ4')
		Endif
    Else
        lRet := .F.        
    EndIf

    If !lRet .Or. !( oModel:VldData() .And. oModel:CommitData() )
		lRet := .F.
        SetRestFault(400, utils.u_getError(oModel:GetErrorMessage()))
	Endif

	oModel:DeActivate()
	FreeObj(oModelZZ4)
	FreeObj(oModel)
Return SetResponse(lRet)

//------------------------------------------------------------------------------
/*/{Protheus.doc} AddMapFields
	Determina quais propriedades x campos do protheus serão apresentados
	@type		function
	@author		Alberto.Gonçalves
	@since		20/10/2024
/*/
//------------------------------------------------------------------------------
Static Function AddMapFields(oSelf as Object)
    oSelf:AddMapFields("filial", "ZZ4_FILIAL", .T., .T., { "ZZ4_FILIAL", 'C', TamSX3('ZZ4_FILIAL')[1] , 0 })
    oSelf:AddMapFields("code"  , "ZZ4_CODE"  , .T., .T., { "ZZ4_CODE"  , 'C', TamSX3('ZZ4_CODE')[1]   , 0 })
    oSelf:AddMapFields("status", "ZZ4_STATUS", .T., .T., { "ZZ4_STATUS", 'C', TamSX3('ZZ4_STATUS')[1] , 0 })
    oSelf:AddMapFields("dtini" , "ZZ4_DTINI" , .T., .F., { "ZZ4_DTINI" , 'D', TamSX3('ZZ4_DTINI')[1]  , 0 })
    oSelf:AddMapFields("dtexpe", "ZZ4_DTEXPE", .T., .F., { "ZZ4_DTEXPE", 'D', TamSX3('ZZ4_DTEXPE')[1] , 0 })
    oSelf:AddMapFields("dtcheg", "ZZ4_DTCHEG", .T., .F., { "ZZ4_DTCHEG", 'D', TamSX3('ZZ4_DTCHEG')[1] , 0 })
    oSelf:AddMapFields("destin", "ZZ4_DESTIN", .T., .F., { "ZZ4_DESTIN", 'C', TamSX3('ZZ4_DESTIN')[1] , 0 })
    
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} SetResponse
	Monta objeto de resposta
	@type		function
	@author		Alberto.Gonçalves
	@since		20/10/2024
/*/
//------------------------------------------------------------------------------
Static Function SetResponse(lRet as Logical) as Json
    Local jResponse as Json

    If lRet
        jResponse := JsonObject():New()
        jResponse['filial'] := RTrim(ZZ4->ZZ4_FILIAL)
        jResponse['code'  ] := RTrim(ZZ4->ZZ4_CODE)
        jResponse['status'] := RTrim(ZZ4->ZZ4_STATUS)
        jResponse['dtini' ] := (ZZ4->ZZ4_DTINI)
        jResponse['dtexpe'] := (ZZ4->ZZ4_DTEXPE)
        jResponse['dtcheg'] := (ZZ4->ZZ4_DTCHEG)
        jResponse['destin'] := RTrim(ZZ4->ZZ4_DESTIN)
        
    EndIf
Return jResponse
